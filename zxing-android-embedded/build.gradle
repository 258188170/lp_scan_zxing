apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.artifactId = 'zxing-android-embedded'

dependencies {
//    api project.zxingCore
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.fragment:fragment:1.3.6'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:1.9.5'
}

android {
    resourcePrefix 'zxing_'
    compileSdkVersion project.androidTargetSdk

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res-orig', 'res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        test.setRoot('test')
    }

    // This is bad practice - we should fix the warnings instead.
    lintOptions {
        // Android warns about the he and id locale folders. he -> iw is already handled with a
        // symlink. TODO: what about id?
        disable 'LocaleFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // We test with primitives such as Rect, and rely on their default behaviour working.
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        minSdkVersion 19
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "lp_zxing.aar" // 指定 AAR 文件名称
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

